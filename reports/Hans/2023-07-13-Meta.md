**Auditors**

[Hans](https://twitter.com/hansfriese)

---

# Findings

## High Risk

### Wrong maths in getNonSlashRate

**Severity:** High

**Context:** [`MetaManager.sol#L102-L107`](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/meta/MetaManager.sol#L102-L107)

**Description:**
The purpose of the `MetaManager::getNonSlashRate` function is to calculate and return the percentage of the deposit that will be redeemed for a given `timeInDays` period. The intended range for the non-slash rate is between 50% and 100%.

However, there is an error in the calculation at line 105. The math is incorrect, and the slope should be in the denominator instead of the numerator.

```solidity
function getNonSlashRate(uint8 timeInDays) internal view returns (uint8) {
    uint256 minDays = minExitCycle / Constants.ONE_DAY;
    uint256 slope = ((maxExitCycle - minExitCycle) * Constants.PINT) / (50 * Constants.ONE_DAY);
    uint256 result = 50 + ((timeInDays - minDays) * slope) / Constants.PINT;//@audit it should be divided by slope and multipled by PINT
    return uint8(result) ;
}
```

**Impact**
The incorrect calculation in the original code can result in users being slashed the wrong amount when unstaking, leading to a loss of assets.

**Recommendation:**

```diff
function getNonSlashRate(uint8 timeInDays) internal view returns (uint8) {
    uint256 minDays = minExitCycle / Constants.ONE_DAY;
    uint256 slope = ((maxExitCycle - minExitCycle) * Constants.PINT) / (50 * Constants.ONE_DAY);
-    uint256 result = 50 + ((timeInDays - minDays) * slope) / Constants.PINT;
+    uint256 result = 50 + ((timeInDays - minDays) * Constants.PINT) / slope ;
    return uint8(result) ;
}
```

**Meta Team:**

Issue is fixed. Added test cases for variety of min & max days. Following the new code implementaion. While the output is unit8, made sure the precision is protected.

```diff
(commit : 007c1b9183cdb65a500928173608ebff0a5197ef)

function getNonSlashRate(uint8 timeInDays) internal view returns (uint8) {
        uint256 minDays = minExitCycle / Constants.ONE_DAY;
+       uint256 slope = (50 * Constants.PINT * Constants.ONE_DAY ) / (maxExitCycle - minExitCycle);
+       uint256 result = 50 + ((timeInDays - minDays) * slope) / Constants.PINT;
        return uint8(result) ;
    }
```

**Hans:**

Verified.

### `Boost.setLockStatus()` should update the caller's rewards first.

**Severity:** High

**Context:** [`Boost.sol#L35-50`](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/reward/Boost.sol#L35-50)

**Description:**
In the rewards contracts(`StakeRewards.sol`, `MintRewards.sol`), the staking rewards are multiplied by the boost factor when the users lock their funds.

And users can extend their lock duration using `setLockStatus()` at any time.

But the rewards before the locking time will be boosted also and the user would claim more rewards.

- At day 0, one user stakes funds using `StakeRewards.stake()`.
- At day 90, he still has the staking funds/rewards and he calls `setLockStatus()` to lock his funds for 30 days.
- At day 120, he can withdraw his staking funds as well as the boosted rewards for 120 days. It's because `earned()` applies the boost to the past rewards as well.

```solidity
    function earned(address _account) public view returns (uint256) {
        return
            ((balanceOf[_account] *
                getBoost(_account) * //@audit apply boost to all rewards
                (rewardPerToken() - userRewardPerTokenPaid[_account])) / 1e38) +
            rewards[_account];
    }
```

The main reason is that `setLockStatus()` doesn't update the user's rewards before starting the boost.

**Impact**
Users might claim more rewards using the boost factor.

**Recommendation:**
The caller's rewards should be updated properly whenever they call `setLockStatus()`.

There are two contracts `StakeRewards.sol` and `MintRewards.sol` that use the boost mechanism.

Currently, `MintRewards.sol` has a `refreshReward()` function but `StakeRewards.sol` doesn't.

We should add the same function to `StakeRewards.sol` and call it inside `setLockStatus()`.

```solidity
    function setLockStatus(uint256 id) external {
        address _user = msg.sender;
        rewardsManager.refreshReward(_user); //@audit rewardsManager should be added by owner

        LockSetting memory _setting = lockSettings[id];
        LockStatus memory userStatus = userLockStatus[_user];
        if (userStatus.unlockTime > block.timestamp) {
            require(
                userStatus.duration <= _setting.duration,
                "Your lock-in period has not ended, and the term can only be extended, not reduced."
            );
        }
        userLockStatus[_user] = LockStatus(
            block.timestamp + _setting.duration,
            _setting.duration,
            _setting.miningBoost
        );
    }
```

**Meta Team:**

Fixed, upon changing the boost status refresh reward function is called, to update the rewards till the point.

```diff

+ address[] public rewardStores;

+    function addRS(address _rs) external onlyOwner {
+        require(_rs != address(0) && _rs != address(this), "Boost: Invalid address");
+        rewardStores.push(_rs);
+    }

+    function removeRS(uint index) external onlyOwner{
+        require(index <= rewardStores.length -1, "Boost: index out of bounds");
+        rewardStores[index] = rewardStores[rewardStores.length - 1];
+       rewardStores.pop();
+    }

+    function updateRewards(address user) internal {
+        for(uint i=0; i<rewardStores.length ; i++) {
+            IRewards(rewardStores[i]).refreshReward(user);
+        }
+    }

+    function getRSSize() external view returns (uint256) {
+       return rewardStores.length ;
+    }

    function setLockStatus(uint256 id) external {
        address _user = msg.sender;
+        updateRewards(_user);
        LockSetting memory _setting = lockSettings[id];
        LockStatus memory userStatus = userLockStatus[_user];
        if (userStatus.unlockTime > block.timestamp) {
            require(
                userStatus.duration <= _setting.duration,
                "Boost: Your lock-in period has not ended, and the term can only be extended, not reduced."
            );
        }
        userLockStatus[_user] = LockStatus(
            block.timestamp + _setting.duration,
            _setting.duration,
            _setting.miningBoost
        );
    }


```

**Hans:**
Verified.

### Final price calculation is wrong in decimal

**Severity:** High

**Context:** [`IDO.sol#L107-L113`](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/ido/IDO.sol#L107-L113)

**Description:**
The final price is updated as follows in `IDO.setFinalPrice`.

```solidity
    price = totalSupply / totalDeposit;
```

The total supply and total deposits are in 18 decimals and `price` will be in wei after it is set in the method.
But the decimal of `price` is supposed to be 18 as we can see in the contructor of `IDO.sol`.

**Impact**
The `price` is used in the `_mint` method, so this will affect the minted amount. The minted amount will be scaled down by 10^18 due to this.

**Recommendation:**

```diff
- price = totalSupply / totalDeposit;
+ price = totalSupply * 1e18 / totalDeposit;
```

**Meta Team:**

Issue is fixed in the latest commit.

```diff
    (commit : 007c1b9183cdb65a500928173608ebff0a5197ef)

    function setFinalPrice() internal {
        if(!computedFinalPrice) {
            uint256 totalDeposit = totalDeposits();
            if( totalDeposit > (totalSupply * Constants.PINT / price)) {
+                price = (totalSupply * Constants.PINT) / totalDeposit;
            }
            computedFinalPrice = true;
        }
    }

```

**Hans:**
Verified.

### Non-accessible function of GMX is used

**Severity:** High

**Context:** [`Helper.sol#L84-89`](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/musd/Helper.sol#L84-89)

**Description:**
`Helper::harvest` calls a GMX router’s `compoundForAccount` function but according to GMX code it’s only callable by governer.

```solidity
    function harvest(address _account) external returns (uint256) {
        router.compoundForAccount(_account);
        gmxRewards.claimForAccount(_account, _account);
        glpRewards.claimForAccount(_account, _account);
        return native.balanceOf(_account);
    }
```

The relevant part on [GMX Router](https://github.com/gmx-io/gmx-contracts/blob/e772060cc46d94fc2679445343f40c65290dede3/contracts/staking/RewardRouterV2.sol#L217 "smartCard-inline") is implemented as below.

```solidity
    function compoundForAccount(address _account) external nonReentrant onlyGov {
        _compound(_account);
    }
```

**Impact**
It is impossible to claim the rewards from the GMX protocol.

**Recommendation:**
Reimplement the harvest mechanism using the correct function of GMX.

**Meta Team:**

Fixed in the latest commit. Harvest function can only be called by the address holding the GLP tokesn. Hence the code has been moved to mUSDManager.sol.

```diff

    (commit: 007c1b9183cdb65a500928173608ebff0a5197ef)

+    interface IGLPRewardsRouter {
+        function claimFees() external;
+    }

+    IGLPRewardsRouter public router;


+    function harvest() public {
+        router.claimFees();
+    }

```

**Hans:**
Verified.

## Medium Risk

### `MetaManager.unclaimedRewards` should work with shares instead of asset amounts.

**Severity:** Medium

**Context:** [`MetaManager.sol#L174-L197`](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/meta/MetaManager.sol#L174-L197)

**Description:**
After the `mUSDManager` calls the `notifyRewardAmount()` function, the rewards are tracked using the `unclaimedRewards` variable when there are no stakers yet.

```solidity
    function notifyRewardAmount(uint amount) external {
        require(msg.sender == address(mUSDManager), "Fund: Invalid caller");
        if (totalStaked() == 0) {
            /**
             * These rewards are unclaimable by the users
             * these tokens are forever locked in the contract
             * Happens if esMETA balance is zero
             *  a) When dApp - launched before IDO
             *  b) When circulation of esMETA is zero (rare-event)
             */
            unclaimedRewards += amount;//@audit should track with shares
            return;
        }
        require(amount > 0, "amount = 0");
        uint256 share = mUSD.getSharesByMintedMUSD(amount);
        rewardPerTokenStored = rewardPerTokenStored + (share * Constants.PINT) / totalStaked();
    }

    function withdrawToTreasury() external onlyOwner {
        require(unclaimedRewards > 0, "Fund: No locked rewards");
        IERC20 _mUSD = IERC20(address(mUSD));
        _mUSD.transfer(treasury, unclaimedRewards); //@audit should transfer shares
        unclaimedRewards = 0;
    }
```

When the owner withdraws the unclaimed rewards using `withdrawToTreasury()`, it transfers the mUSD amounts.

But `mUSD._transfer()` works with the shares and the ratio between shares and amounts might be changed at any time.

```solidity
     function _transfer(address _sender, address _recipient, uint256 _amount) internal virtual {
        uint256 _sharesToTransfer = getSharesByMintedMUSD(_amount);
        _transferShares(_sender, _recipient, _sharesToTransfer);
        emit Transfer(_sender, _recipient, _amount);
        emit TransferShares(_sender, _recipient, _sharesToTransfer);
    }
```

So during the withdrawal of unclaimed rewards, more or less mUSD than the expected amount might be withdrawn.

**Impact**
Some rewards might be locked inside the contract or stakers might fail to claim the rewards as the contract doesn't have enough rewards. Because the stakers' rewards wouldn't be preserved inside the contract without any additional assumptions, I evaluate the severity to High.

**Recommendation:**
`unclaimedRewards` should be tracked with shares.

```solidity
    function notifyRewardAmount(uint amount) external {
        require(msg.sender == address(mUSDManager), "Fund: Invalid caller");
        if (totalStaked() == 0) {
            /**
             * These rewards are unclaimable by the users
             * these tokens are forever locked in the contract
             * Happens if esMETA balance is zero
             *  a) When dApp - launched before IDO
             *  b) When circulation of esMETA is zero (rare-event)
             */
            unclaimedRewards += mUSD.getSharesByMintedMUSD(amount); //@audit add shares
            return;
        }
        require(amount > 0, "amount = 0");
        uint256 share = mUSD.getSharesByMintedMUSD(amount);
        rewardPerTokenStored = rewardPerTokenStored + (share * Constants.PINT) / totalStaked();
    }

    function withdrawToTreasury() external onlyOwner {
        require(unclaimedRewards > 0, "Fund: No locked rewards");
        IERC20 _mUSD = IERC20(address(mUSD));
        _mUSD.transferShares(treasury, unclaimedRewards); //@audit transfer shares
        unclaimedRewards = 0;
    }
```

**Meta Team:**

Issue is fixed. Unclaimed rewards are handled in shares for consistency of accounting and transfers.

```diff
(commit : 007c1b9183cdb65a500928173608ebff0a5197ef)

function notifyRewardAmount(uint amount) external {
        require(msg.sender == address(mUSDManager), "Fund: Invalid caller");
        if (totalStaked() == 0) {
            /**
             * These rewards are unclaimable by the users
             * these tokens are forever locked in the contract
             * Happens if esMETA balance is zero
             *  a) When dApp - launched before IDO
             *  b) When circulation of esMETA is zero (rare-event)
             */
+           unclaimedRewards += mUSD.getSharesByMintedMUSD(amount);
            return;
        }
        require(amount > 0, "amount = 0");
        uint256 share = mUSD.getSharesByMintedMUSD(amount);
        rewardPerTokenStored = rewardPerTokenStored + (share * Constants.PINT) / totalStaked();
    }

    function withdrawToTreasury() external onlyOwner {
        require(unclaimedRewards > 0, "Fund: No unclaimed rewards");
+       mUSD.transferShares(treasury, unclaimedRewards);
        unclaimedRewards = 0;
    }
```

**Hans:**
Verified.

### Chainlink price is used without checking validity

**Severity:** Medium

**Context:** [`Helper.sol#L75-L78`](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/musd/Helper.sol#L75-L78)

**Description:**
The Meta protocol relies on a Chainlink price oracle to calculate the excess income distributed to all mUSD holders.
However, the current implementation lacks checks for the staleness of the price obtained from Chainlink.

```solidity
function getPriceOfRewardToken() external view returns (uint256) {
    (,int256 price,,,) = priceFeed.latestRoundData();//@audit chainlink price feed - stale price check is missing
    return (uint256(price) * Constants.PINT) / PRICE_FEED_PRECISION;
}
```

This omission can lead to issues if Chainlink starts a new round and struggles to establish consensus on the new value for the oracle. Without proper checks, consumers of this contract may continue using outdated, stale, or incorrect data if oracles are unable to submit and start a new round. Possible reasons for this could include Chainlink nodes abandoning the oracle, chain congestion, or vulnerabilities/attacks on the Chainlink system.

Additionally, it is important to check if the Arbitrum sequencer is active.
Please refer to the issue at https://github.com/sherlock-audit/2022-11-sentiment-judging/issues/3 for more information.

**Impact**
This vulnerability is classified as MEDIUM because it affects user assets only when the Chainlink oracle is in bad status.

**Recommendation:**
To address this issue, it is recommended to implement checks to ensure that the price returned by Chainlink is not stale. The following code snippet can be used to validate the price obtained from Chainlink:

```solidity
( roundId, rawPrice, , updateTime, answeredInRound ) = priceFeed.latestRoundData();
require(rawPrice > 0, "Chainlink price <= 0");
require(updateTime != 0, "Incomplete round");
require(answeredInRound >= roundId, "Stale price");
```

**Meta Team:**

Fixed in the latest commit. Added the checks on the price value and also sequencer validation.

```diff

+    function isSequencerActive() internal view returns (bool) {
+        (, int256 answer, uint256 startedAt,,) = sequencer.latestRoundData();
+        if (block.timestamp - startedAt <= GRACE_PERIOD_TIME || answer == 1)
+            return false;
+        return true;
+    }

   function getPriceOfRewardToken() external view returns (uint256) {
        (uint80 roundId,int256 price,,uint256 updateTime, uint80 answeredInRound) = priceFeed.latestRoundData();
+       require(isSequencerActive(), "HLP: Sequencer is down");
+       require(price > 0, "HLP: Invalid chainlink price");
+       require(updateTime > 0, "HLP: Incomplete round");
+       require(answeredInRound >= roundId, "HLP: Stale price");
        return (uint256(price) * Constants.PINT) / PRICE_FEED_PRECISION;
    }

```

**Hans:**
Verified.

### Some constants wouldn't work as expected.

**Severity:** Medium

**Context:** [`Constants.sol#L5`](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/lib/Constants.sol#L5)

**Description:**
Two constants `KEEPER_RATE` and `MAX_KEEPER_RATE` are declared like the below.

```solidity
    uint256 constant KEEPER_RATE = 0.01e18; //1%
    uint256 constant MAX_KEEPER_RATE = 0.05e18; //5%
```

But when we use the `KEEPER_RATE` in `liquidation()/superLiquidation()`, we divide the rate by 100 again.

```solidity
    if (provider == msg.sender) {
        glp.transfer(msg.sender, reducedGLP);
    } else {
        reward2keeper = (reducedGLP * rates.getKR()) / 110 / Constants.PINT; //@audit divide by 100 again
        glp.transfer(provider, reducedGLP - reward2keeper);
        glp.transfer(msg.sender, reward2keeper);
    }
```

So the default keeper rate will be 0.01% instead of 1% during the liquidation.

**Impact**
The reward rate for the keepers wouldn't work as expected. Because it will just affect the reward rate for keepers without losing funds, I evaluate the severity to Medium.

**Recommendation:**
Modify the constants or reward calculation formula accordingly.

**Meta Team:**

The keepr rewards and number libraries are consistenly used. And necessary changes have been made in the commit 007c1b9183cdb65a500928173608ebff0a5197ef

```
Constants.sol

library Constants {
    // Base constants
    uint256 constant PINT = 1e18;
    uint256 constant HUNDRED_PERCENT = 100e18; //100

    // Collateral constants
    uint256 constant BAD_COLLATERAL_LIMIT = 150e18; //150
    uint256 constant SAFE_COLLATERAL_LIMIT = 160e18; //160
    uint256 constant SUPER_BAD_COLLATERAL_LIMIT = 125e18; //125
    uint256 constant REDEMPTION_PROVIDER_CR = 100e18; //100

    // Reward constants
    uint256 constant REDEEMER_BOOST_RATE = 20e18; //20% Boost
    uint256 constant MAX_EXCESS_DISTRIBUTION_REWARD = 0.5e18; //0.5%

    // Fee & Rate constants
    uint256 constant MAX_HOLDING_FEE = 1.5e18; //1.5%
    uint256 constant REDEMPTION_FEE = 0.5e18; //0.5%
    uint256 constant KEEPER_RATE = 1e18; //1%
    uint256 constant MAX_KEEPER_RATE = 5e18; //5%
    uint256 constant MAX_TREASURY_FEE = 30e18; //30%

    // Supply constants
    uint256 constant META_MAX_SUPPLY = 100_000_000 * 1e18;
    uint256 constant ESMETA_MAX_SUPPLY = 100_000_000 * 1e18;

    // Constants for Meta Manager & Time periods
    uint256 constant YEAR_IN_SECONDS = 365 days;
    uint256 constant VESTING_PERIOD = 60 days;
    uint256 constant REWARDS_PAYOUT_PERIOD = 30 days;
    uint256 constant ONE_DAY = 1 days;

    // Numerical constant/s
    uint256 constant HUNDERED = 100;
}

```

**Hans:**
Verified.

### On restaking, funds should be unslashed back

**Severity:** Medium

**Context:** [`MetaManager.sol#L117-L126`](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/meta/MetaManager.sol#L117-L126)

**Description:**
In `MetaManager::reStake`, `toMint` is calculated as `getReservedForVesting(caller) + getClaimable(caller)` and it’s essentially the same to `unstakeRate[_user] * (time2fullRedemption[_user] - lastWithdrawTime[_user])`. The problem is that this should be “unslashed” using the `lastSlashRate` to be fair.

For example, a user starts unstaking 100e18 esMETA with 10 days vesting period and after a day he decided to stop unstaking and restake.

In this case, the originally slashed 50e18 esMETA are not fair for him and the protocol should reimburse partial slash.

**Impact**
While this is not a genuine bug, it is more of a recommendation to enhance the protocol's completeness. However, I have classified its severity level as MEDIUM since it is evidently not an intended mechanism to ensure fairness.

**Recommendation:**
Add a new logic to reimburse the slashed amounts partially when the user restakes.

**Meta Team:**

Fixed.

```diff

function reStake() external updateReward(msg.sender) {
        address caller = msg.sender;
        uint256 toMint = getReservedForVesting(caller) + getClaimable(caller);
+       toMint = (toMint * 100 * Constants.PINT) / (100 - lastSlashRate[caller]);
+       toMint /= Constants.PINT;
        if (toMint > 0) {
            esMeta.mint(caller, toMint);
            unstakeRate[caller] = 0;
            time2fullRedemption[caller] = 0;
            lastSlashRate[caller] = 0;
        }
    }
```

**Hans:**
Verified.

### Admin level vulnerabilities

**Severity:** Medium

**Context:** [`IDO.sol#L66-69`](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/ido/IDO.sol#L66-69)

**Description:**
Numerous admin functions do not check the validity of the input parameters.

- Many setter functions that set token addresses do not validate zero address (e.g. `MetaManager::setTokens`)
- `IDO::setClaimTime` - time validation
- `MetaManager::setMaxExitCycle` - use strict inequality to prevent DOS unstaking

Also some functions are not necessary and can lead to unintentional situations.

- `IDO::setPrice` - According to the documentation, the floor price is kept as constant but the current implementation allow the owner to change the price to any value.

Although we assume the admin is trusted, these issues can lead to unexpected loss by a mistake of an admin.

**Impact**
The admin can change the protocol’s behavior in unexpected ways.

**Recommendation:**
Add necessary validations to the admin functions and remove unnecessary functions.

**Meta Team:**

Fixed. In the commit :007c1b9183cdb65a500928173608ebff0a5197ef.
Actions include require statments and also to remove unnecessary functions.

**Hans:**
Verified.

### Borrower with bad debt gets mint reward

**Severity:** Medium

**Context:** [`MUSDManager.sol#L249`](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/musd/MUSDManager.sol#L249), [`MUSDManager.sol#L299`](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/musd/MUSDManager.sol#L299 "‌"), [`MintRewards.sol#L121`](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/reward/MintRewards.sol#L121 "‌"), [`MintRewards.sol#L92`](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/reward/MintRewards.sol#L92 "‌")

**Description:**

With the current implementation, it is possible that the borrowers are in bad debt. As confirmed with the protocol team, _“When user's collateral position is under 100%, only the respective amount of mUSD which is equivalent of GLP\*price which is less than is debt position is closed.”_ ([MUSDManager.sol#L249](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/musd/MUSDManager.sol#L249), [MUSDManager.sol#L299](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/musd/MUSDManager.sol#L299 "‌"))

On the other hand, by the protocol’s design, anyone with positive `borrowed` is considered as a mUSD holder and can claim the mint reward. Note that the user does not need to mint additional mUSD. ([MintRewards.sol#L121](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/reward/MintRewards.sol#L121 "‌"), [MintRewards.sol#L92](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/reward/MintRewards.sol#L92 "‌"))

```solidity
function earned(address _account) public view returns (uint256) {
    return
        ((stakedOf(_account) *
            getBoost(_account) *
            (rewardPerToken() - userRewardPerTokenPaid[_account])) / 1e38) +
        rewards[_account];
}

function stakedOf(address user) public view returns (uint256) {
    return mUSDManager.getBorrowedOf(user);
}
```

This means the mint reward can be claimed by the borrower who has zero deposit and other regular minters get less reward.

**Impact**
Borrower with zero deposit and bad debt can claim the mint reward and healthy mUSD holders get less reward.

**Recommendation:**
Clear the bad debt when the deposit becomes zero and track a cumulative of that bad debt amount. This cumulative value can be used in calculation of `balanceOf` and `totalSupply`.

**Meta Team:**

Fixed. Total bad debt is maintained at mUSDManager.sol. Treasury / prortocol / admin can remove the bad debt by contributing their tokes. Treasury will take the primary responsibility in ensuring system does not have bad debt.

```diff
mUSDManager.sol
(commit : 007c1b9183cdb65a500928173608ebff0a5197ef)


    // contract level variable
+    uint256 public totalBadDebt;

    function superLiquidation(
        address provider,
        address onBehalfOf,
        uint256 glpAmount
    ) external whenNotPaused nonReentrant {

...
+        if(deposited[onBehalfOf] == 0 && borrowed[onBehalfOf] > 0) { // Accounting bad debt
+        totalBadDebt += borrowed[onBehalfOf];
+        borrowed[onBehalfOf] = 0;
+    }
...
    }

    // function through debt will be repaid
+ function repayBadDebt(uint256 _amount) public {
+    require(totalBadDebt >= _amount, "Low debt");
+    mUSD.burnShares(msg.sender, mUSD.getSharesByMintedMUSD(_amount), _amount);
+    totalBadDebt -= _amount;
+ }
```

**Hans:**
Verified.

### Rounding error in unstaking esMeta

**Severity:** Medium

**Context:** [`MetaManager.sol#L84`](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/meta/MetaManager.sol#L84)

**Description:**
Users can start unstaking esMETA by calling `MetaManager::unstake` providing the amount and vesting period in days.

[https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/meta/MetaManager.sol#L84](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/meta/MetaManager.sol#L84 "‌")

For simplicity of discussion, let us see the first unstaking process.
According to the provided vesting period, `nonSlashRate` is calculated and `unstakeRate` is calculated as `amount * nonSlashRate / 100 / timeToExit`.

In this calculation, `amount` is in 18 decimals, `nonSlashRate` ranges 50~100 and `timeToExit` ranges from `minExitCycle` ~ `maxExitCycle` (10~50 days by default but can be changed by admin).

Let us say a user stakes 1000e18 GLP to get 1000e18 esMETA.
Later he decides to unstake the whole amount for the next 50 days.

```
unstakeRatio = amount / timeToExit = 1000e18/(50 days) = 231481481481481
```

After 50 days, he calls `withdraw()` and the claimable amount after full vesting is :

```
unstakeRatio * 50 days = 231481481481481 * 50 days = 999999999999997920000
```

The received amount is `2080000` wei lower than expected.

```
1000e18 - 999999999999996192000 = 2080000
```

Although this amount seems quite low cause the GLP is in 18 decimals but this will be happen across users for all unstaking process. As a result, this error will gradually increase and cause inaccuracy.

**Impact**
Users will always get less amount than expected on full unstaking and the errors are accumulated to cause the imbalance of META and esMETA.

**Recommendation:**
Extend the accuracy of `unstakeRatio` by 1e18 and divide the claimable amount back by 1e18.

```
function unstake(uint256 _amount, uint8 _timeInDays) external updateReward(msg.sender) {
  // ...
  unstakeRate[caller] = total * 1e18 / timeToExit;
  //
}

function getClaimable(address _user) public view returns (uint256 amount) {//@audit-info time windowed rewards
    if (time2fullRedemption[_user] > lastWithdrawTime[_user]) {//@audit-info user already withdrawed after full redemption time, no rewards are available to claim
        amount = block.timestamp > time2fullRedemption[_user]
            ? unstakeRate[_user] *
                (time2fullRedemption[_user] - lastWithdrawTime[_user]) / 1e18
            : unstakeRate[_user] *
                (block.timestamp - lastWithdrawTime[_user]) / 1e18;
    }
}
```

**Meta Team:**

Fixed. Precision losses are addressed.

```diff

function unstake(uint256 _amount, uint8 _timeInDays) external updateReward(msg.sender) {
        address caller = msg.sender;
        require(_timeInDays >= minExitCycle/Constants.ONE_DAY && _timeInDays <= maxExitCycle/Constants.ONE_DAY, "Fund: Invalid vesting days");

        esMeta.burn(caller, _amount);
        _withdraw(caller);
        uint256 total = _amount;
        if (time2fullRedemption[caller] > block.timestamp) {
            uint256 scaled = ((unstakeRate[caller] * 100 ) * (time2fullRedemption[caller] - block.timestamp)) / (100 - lastSlashRate[caller]);
            scaled /= Constants.PINT;
            total += scaled ;
        }
        uint8 nonSlashRate = getNonSlashRate(_timeInDays);
        total = (total  * nonSlashRate)/100;
        uint256 timeToExit = _timeInDays * Constants.ONE_DAY;
        lastSlashRate[caller] = 100 - nonSlashRate;
+        unstakeRate[caller] = (total * Constants.PINT) / timeToExit;
        time2fullRedemption[caller] = block.timestamp + timeToExit;
    }


function getClaimable(address _user) public view returns (uint256 amount) {
        if (time2fullRedemption[_user] > lastWithdrawTime[_user]) {
            amount = block.timestamp > time2fullRedemption[_user]
                ? unstakeRate[_user] *
                    (time2fullRedemption[_user] - lastWithdrawTime[_user])
                : unstakeRate[_user] *
                    (block.timestamp - lastWithdrawTime[_user]);
+            amount /= Constants.PINT;
        }
    }
```

**Hans:**
Verified.

## Low Risk

### Wrong values or confusing words in event messages

**Severity:** Low

**Description:**

- At [mUSD.sol#L146](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/musd/mUSD.sol#L146), `postRebaseTokenAmount` should be calculated after decreasing `totalMUSDCirculation`.
- At [IDO.sol#L103](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/ido/IDO.sol#L103), the event message is confusing. I recommend changing the message to `"IDO: Multisig account can not be this contract"`.

**Meta Team:**
Fixed.
(commit 007c1b9183cdb65a500928173608ebff0a5197ef)

**Hans:**
Verified.

### Use modifier instead of repeating the same code block

**Severity:** Low

**Description:**

There are numerous places where the same code block is repeated. These can be replaced with a modifier.

- [IDO.sol#L92](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/ido/IDO.sol#L92), [IDO.sol#L129](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/ido/IDO.sol#L129), [IDO.sol#L134](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/ido/IDO.sol#L134)

- [IDO.sol#L160](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/ido/IDO.sol#L160), [IDO.sol#L173](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/ido/IDO.sol#L173), [IDO.sol#L181](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/ido/IDO.sol#L181)

- [ESMeta.sol#L35](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/meta/ESMeta.sol#L35), [ESMeta.sol#L50](https://github.com/getmetafinance/meta/blob/00bbac1613fa69e4c180ff53515451df4df9f69e/contracts/meta/ESMeta.sol#L50)

**Meta Team:**

Fixed. Either refactored the code or used modifiers.

(commit : 007c1b9183cdb65a500928173608ebff0a5197ef)

**Hans:**
Verified.

## Automated Findings

Below are findings that were discovered through automated static analysis of this project's source code.
A static analyzer [4naly3er](https://github.com/Picodes/4naly3er) was used to perform these checks.

### Non Critical Issues

#### <a name="NC-1"></a>[NC-1] Missing checks for `address(0)` when assigning values to address state variables

_Instances (8)_:

```solidity
File: meta/Meta.sol

14:         metaMgr = _metaMgr;

18:         metaMgr = _metaMgr;

```

```solidity
File: musd/Helper.sol

63:         glpRewardsStore = _glpRewardsStore;

64:         gmxRewardsStore = _gmxRewardsStore;

```

```solidity
File: util/Allowed.sol

15:         _ownerAddr = owner;

36:         _ownerAddr = _owner;

```

```solidity
File: util/Governable.sol

13:         gov = owner;

26:         gov = _gov;

```

**Meta Team:**

Added the validations, commit : 007c1b9183cdb65a500928173608ebff0a5197ef

**Hans:**
Verified.

#### <a name="NC-2"></a>[NC-2] `require()` / `revert()` statements should have descriptive reason strings

_Instances (1)_:

```solidity
File: ido/IDO.sol

190:         require(block.timestamp >= endTime);

```

**Meta Team:**

Added the error message, commit : 007c1b9183cdb65a500928173608ebff0a5197ef

**Hans:**
Verified.

#### <a name="NC-3"></a>[NC-3] Return values of `approve()` not checked

Not all IERC20 implementations `revert()` when there's a failure in `approve()`. The function signature has a boolean return value and they indicate errors that way instead. By not checking the return value, operations that should have marked as failed, may potentially go through without actually approving anything

_Instances (2)_:

```solidity
File: musd/mUSD.sol

83:         _approve(msg.sender, _spender, _amount);

125:         _approve(_sender, msg.sender, (currentAllowance - _amount));

```

**Meta Team:**

Line:116 protects from approval failures, as the allowance is checked.

**Hans:**
Verified.

#### <a name="NC-4"></a>[NC-4] Event is missing `indexed` fields

Index event fields make the field more quickly accessible to off-chain tools that parse events. However, note that each index field costs extra gas during emission, so it's not necessarily best to index the maximum allowed per event (three fields). Each event should use three indexed fields if there are three or more fields, and gas usage is not particularly of concern for the events in question. If there are fewer than three fields, all of the fields should be indexed.

_Instances (18)_:

```solidity
File: mocks/WETH.sol

8:     event Received(address indexed sender, uint256 amount);

```

```solidity
File: musd/MUSDManager.sol

38:     event Deposit(address indexed user, uint256 amount, uint256 timestamp);

39:     event Withdraw(address indexed user, uint256 amount, uint256 timestamp);

40:     event Mint(

46:     event Burn(

52:     event RedemptionProvider(address user, bool status);

53:     event FeeDistributed(

58:     event InterestDistributed(

63:     event RigidRedemption(

70:     event LiquidationRecord(

```

```solidity
File: musd/interface/IRates.sol

34:     event HoldingFeeChanged(uint256);

35:     event SafeCollateralRateChanged(uint256);

36:     event BadCollateralRateChanged(uint256);

37:     event KeeperRateChanged(uint256);

38:     event RedemptionFeeChanged(uint256);

```

```solidity
File: musd/mUSD.sol

18:     event SharesBurnt(address indexed account, uint256 pre, uint256 post, uint256 sharesAmount);

19:     event TransferShares(address indexed from, address indexed to, uint256 sharesValue);

```

```solidity
File: util/Governable.sol

10:     event GovernanceAuthorityTransfer(address newGov);

```

**Meta Team:**

All addresses in the event are indexed.

**Hans:**
Verified.

#### <a name="NC-5"></a>[NC-5] Constants should be defined rather than using magic numbers

_Instances (5)_:

```solidity
File: meta/MetaManager.sol

104:         uint256 slope = ((maxExitCycle - minExitCycle) * Constants.PINT) / (50 * Constants.ONE_DAY);

```

```solidity
File: reward/Boost.sol

25:         lockSettings.push(LockSetting(30 days, 20 * Constants.PINT));

26:         lockSettings.push(LockSetting(90 days, 30 * Constants.PINT));

27:         lockSettings.push(LockSetting(180 days, 50 * Constants.PINT));

28:         lockSettings.push(LockSetting(365 days, 100 * Constants.PINT));

```

**Meta Team:**
Added all the numbers as constants

**Hans:**
Verified.

#### <a name="NC-6"></a>[NC-6] Functions not used internally could be marked external

_Instances (21)_:

```solidity
File: ido/IDO.sol

194:     function getDepositOf(address _user) public view returns (uint256) {

198:     function ethBalance(address _user) public view returns (uint256) {

```

```solidity
File: meta/MetaManager.sol

37:     function setMinExitCycle(uint256 _minExitCycle) public onlyOwner {

42:     function setMaxExitCycle(uint256 _maxExitCycle) public onlyOwner {

47:     function setTreasury(address _treasury) public onlyOwner {

```

```solidity
File: mocks/WETH.sol

27:     function unwrap(uint256 _amount) public returns (uint256) {

```

```solidity
File: musd/MUSDManager.sol

86:     function setHelper(address _helper) public onlyOwner {

90:     function setRates(address _rates) public onlyOwner {

94:     function setMetaManager(address _metaMgr) public onlyOwner {

98:     function setMRewards(address _mRewards) public onlyOwner {

177:     function mint(uint256 _mUSDAmount) public whenNotPaused nonReentrant {

```

```solidity
File: musd/mUSD.sol

30:     function setmUSDManager(address _mUSDManager) public onlyOwner {

54:     function symbol() public pure override returns (string memory) {

58:     function decimals() public pure override returns (uint8) {

70:     function getTotalShares() public view returns (uint256) {

150:     function transferShares(address _recipient, uint256 _sharesAmount) public returns (uint256) {

```

```solidity
File: util/Allowed.sol

35:     function setOwner(address _owner) public onlyOwner returns (bool) {

40:     function grantRole(address _user) public onlyOwner returns (bool) {

45:     function revokeRole(address _user) public onlyOwner returns (bool) {

50:     function getOwner() public view returns(address) {

54:     function isAllowed(address user) public view returns (bool) {

```

**Meta Team:**

Marked the functions appropriately based on the usage.

**Hans:**
Verified.

### Low Issues

#### <a name="L-1"></a>[L-1] `abi.encodePacked()` should not be used with dynamic types when passing the result to a hash function such as `keccak256()`

Use `abi.encode()` instead which will pad items to 32 bytes, which will [prevent hash collisions](https://docs.soliditylang.org/en/v0.8.13/abi-spec.html#non-standard-packed-mode) (e.g. `abi.encodePacked(0x123,0x456)` => `0x123456` => `abi.encodePacked(0x1,0x23456)`, but `abi.encode(0x123,0x456)` => `0x0...1230...456`). "Unless there is a compelling reason, `abi.encode` should be preferred". If there is only one argument to `abi.encodePacked()` it can often be cast to `bytes()` or `bytes32()` [instead](https://ethereum.stackexchange.com/questions/30912/how-to-compare-strings-in-solidity#answer-82739).
If all arguments are strings and or bytes, `bytes.concat()` should be used instead

_Instances (1)_:

```solidity
File: ido/IDO.sol

50:                 keccak256(abi.encodePacked(msg.sender))

```

**Meta Team:**

In the current usecase, only user address will be passed hence encodePacked is used.

**Hans:**
Acknowledged.

#### <a name="L-2"></a>[L-2] Empty Function Body - Consider commenting why

_Instances (5)_:

```solidity
File: meta/MetaManager.sol

35:     constructor() Allowed(msg.sender) {}

163:     function refreshReward(address _account) external updateReward(_account) {}

```

```solidity
File: mocks/WETH.sol

13:     constructor() Token('WETH Token', 'WETH', 18) {}

```

```solidity
File: musd/mUSD.sol

23:     constructor() Allowed(msg.sender) {}

```

```solidity
File: reward/MintRewards.sol

103:     function refreshReward(address _account) external updateReward(_account) {}

```

**Meta Team:**

Functions comments are added.

**Hans:**
Verified.

#### <a name="L-3"></a>[L-3] Unsafe ERC20 operation(s)

_Instances (18)_:

```solidity
File: ido/IDO.sol

123:             weth.transferFrom(caller, address(this), _wethAmount);

163:             weth.transfer(mulSig, balanceOfWEth()); // send wETH

184:             weth.transfer(mulSig, balanceOfWEth()); // send wETH

191:         IERC20(token).transfer(to, amount);

```

```solidity
File: meta/MetaManager.sol

195:         _mUSD.transfer(treasury, unclaimedRewards);

```

```solidity
File: musd/MUSDManager.sol

150:         glp.transferFrom(caller, address(this), _amount);

173:         glp.transfer(caller, _amount);

253:             glp.transfer(msg.sender, reducedGLP);

256:             glp.transfer(provider, reducedGLP - reward2keeper);

257:             glp.transfer(msg.sender, reward2keeper);

304:             glp.transfer(msg.sender, reward2keeper);

306:         glp.transfer(provider, glpAmount - reward2keeper);

344:         glp.transfer(caller, glpAmount);

369:             mUSD.transferFrom(

381:             mUSD.transferFrom(

391:         rt.transfer(provider, payoutEther);

```

```solidity
File: reward/StakeRewards.sol

93:         stakingToken.transferFrom(caller, address(this), _amount);

107:         stakingToken.transfer(caller, _amount);

```

**Meta Team:**

Using the underlying transfer and transferFrom functions of the token.

**Hans:**
Verified.

### Medium Issues

#### <a name="M-1"></a>[M-1] Centralization Risk for trusted owners

##### Impact:

Contracts have owners with privileged rights to perform admin tasks and need to be trusted to not perform malicious updates or drain funds.

_Instances (38)_:

```solidity
File: ido/IDO.sol

62:     function setMerkleRoot(bytes32 _root) external onlyOwner {

66:     function setPrice(uint256 _price) external onlyOwner {

71:     function setMinBuy(uint256 _minBuy) external onlyOwner {

76:     function setMaxBuy(uint256 _maxBuy) external onlyOwner {

81:     function setStartTime(uint256 _start) external onlyOwner {

86:     function setEndTime(uint256 _end) external onlyOwner {

96:     function setWLBoost(uint256 _wlBoost) external onlyOwner {

101:     function setMulSigAcc(address _account) external onlyOwner {

159:     function withdrawAll() external onlyOwner {

171:     function withdrawEth() external onlyOwner {

180:     function withdrawWEth() external onlyOwner {

188:     function sendToken(address token, address to, uint256 amount) external onlyOwner {

```

```solidity
File: meta/Meta.sol

17:     function setMetaManager(address _metaMgr) external onlyOwner {

```

```solidity
File: meta/MetaManager.sol

37:     function setMinExitCycle(uint256 _minExitCycle) public onlyOwner {

42:     function setMaxExitCycle(uint256 _maxExitCycle) public onlyOwner {

47:     function setTreasury(address _treasury) public onlyOwner {

53:     function setMUSD(address _mUSD) external onlyOwner {

59:     function setMUSDManager(address _mUSDManager) external onlyOwner {

65:     function setTokens(address _meta, address _esMeta) external onlyOwner {

192:     function withdrawToTreasury() external onlyOwner {

```

```solidity
File: musd/MUSDManager.sol

86:     function setHelper(address _helper) public onlyOwner {

90:     function setRates(address _rates) public onlyOwner {

94:     function setMetaManager(address _metaMgr) public onlyOwner {

98:     function setMRewards(address _mRewards) public onlyOwner {

```

```solidity
File: musd/mUSD.sol

30:     function setmUSDManager(address _mUSDManager) public onlyOwner {

```

```solidity
File: reward/Boost.sol

9: contract Boost is IBoost, Ownable {

31:     function addLockSetting(LockSetting memory setting) external onlyOwner {

```

```solidity
File: reward/MintRewards.sol

51:     function setEsMETA(address _esMeta) external onlyOwner {

55:     function setExtraRate(uint256 rate) external onlyOwner {

59:     function setBoost(address _boost) external onlyOwner {

63:     function setRewardsDuration(uint256 _duration) external onlyOwner {

142:     function notifyRewardAmount(uint256 amount) external onlyOwner updateReward(address(0)) {

```

```solidity
File: reward/StakeRewards.sol

65:     function setBoost(address _boost) external onlyOwner {

69:     function setRewardsDuration(uint256 _duration) external onlyOwner {

143:     function notifyRewardAmount(uint256 _amount) external onlyOwner updateReward(address(0)) {

```

```solidity
File: util/Allowed.sol

35:     function setOwner(address _owner) public onlyOwner returns (bool) {

40:     function grantRole(address _user) public onlyOwner returns (bool) {

45:     function revokeRole(address _user) public onlyOwner returns (bool) {

```

**Meta Team:**

Acknowledged, admin is trusted as the protocol owner will be a multisig account.

**Hans:**

Acknowledged.
