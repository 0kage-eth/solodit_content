**Auditor**

[Pashov Audit Group](https://twitter.com/PashovAuditGrp)

# Findings

## Medium Risk

### [M-01] Protocol charges platform fees twice

**Severity**

**Impact:** High

**Likelihood:** Low

**Description**

LP can call the repay on an underwater loan to retrieve their tokens back without their position being restored. If there are no other positions associated with this loan, the caller will receive the collateral minus platform fees and a liquidation bonus. However, there is an issue where platform fees are being charged twice.

```solidity
        if (params.isEmergency) {
            (!underLiquidation).revertError(ErrLib.ErrorCode.FORBIDDEN);
            (
                uint256 removedAmt,
                uint256 feesAmt,
                bool completeRepayment
            ) = _calculateEmergencyLoanClosure(
                    zeroForSaleToken,
                    params.borrowingKey,
                    currentFees,
                    borrowing.borrowedAmount
                );
            (removedAmt == 0).revertError(ErrLib.ErrorCode.LIQUIDITY_IS_ZERO);
            // Subtract the removed amount and fees from borrowedAmount and feesOwed
            borrowing.borrowedAmount -= removedAmt;
            borrowing.dailyRateCollateralBalance -= feesAmt;
>>          feesAmt =
                _pickUpPlatformFees(borrowing.holdToken, feesAmt) /
                Constants.COLLATERAL_BALANCE_PRECISION;
            // Deduct the removed amount from totalBorrowed
            unchecked {
                holdTokenRateInfo.totalBorrowed -= removedAmt;
            }
            // If loansInfoLength is 0, remove the borrowing key from storage and get the liquidation bonus
            if (completeRepayment) {
                LoanInfo[] memory empty;
                _removeKeysAndClearStorage(borrowing.borrower, params.borrowingKey, empty);
>>              feesAmt =
                  _pickUpPlatformFees(borrowing.holdToken, currentFees) /
                    Constants.COLLATERAL_BALANCE_PRECISION +
                    liquidationBonus;
            } else {
```

This will break protocol accounting since the recorded sum of tokens will be greater than the actual amount.

Here is the coded POC in `LiquidityBorrowingManager.t.sol`:

```solidity
    function testDoublePlatformFee() public {
        uint128 minLiqAmt = _minimumLiquidityAmt(253_320, 264_600);
        address[] memory tokens = new address[](1);
        tokens[0] = address(WETH);
        address vault = borrowingManager.VAULT_ADDRESS();

        vm.startPrank(bob);
        borrowingManager.borrow(createBorrowParams(tokenId, minLiqAmt), block.timestamp + 1);
        bytes32[] memory key = borrowingManager.getBorrowingKeysForTokenId(tokenId);
        vm.stopPrank();

        ILiquidityBorrowingManager.FlashLoanRoutes memory routes;
        ILiquidityBorrowingManager.SwapParams[] memory swapParams;

        ILiquidityBorrowingManager.RepayParams memory repay = ILiquidityBorrowingManager.RepayParams({
            isEmergency: true,
            routes: routes,
            externalSwap: swapParams,
            borrowingKey: key[0],
            minHoldTokenOut: 0,
            minSaleTokenOut: 0
        });

        // time to repay underwater loan
        vm.warp(block.timestamp + 86401);
        vm.prank(alice);
        (uint saleOut, uint holdToken) = borrowingManager.repay(repay, block.timestamp + 1);

        borrowingManager.collectProtocol(address(this), tokens);

        vm.expectRevert(bytes("W-ST"));
        vm.prank(alice);
        borrowingManager.collectLoansFees(tokens);
    }
```

In this scenario LP is unable to collect the rewards after platform fees were collected.

**Recommendations**

```diff
            if (completeRepayment) {
                LoanInfo[] memory empty;
                _removeKeysAndClearStorage(borrowing.borrower, params.borrowingKey, empty);
+               feesAmt +=
-                   _pickUpPlatformFees(borrowing.holdToken, currentFees) /
-                   Constants.COLLATERAL_BALANCE_PRECISION +
                    liquidationBonus;
            } else {
```

## Low Risk

### [L-01] Users might overpay while borrowing

The final cost of borrowing is determined by taking a sum of `marginDeposit`, `liquidationBonus`, `dailyRateCollateral` and `holdTokenEntranceFee`.

```solidity
// LiquidityBorrowingManager.sol

uint256 amountToPay;
unchecked {
    // Updating borrowing details
    borrowing.borrowedAmount += cache.borrowedAmount;
    borrowing.liquidationBonus += liquidationBonus;
    // Transfer the required tokens to the VAULT_ADDRESS for collateral and holdTokenBalance
    borrowing.dailyRateCollateralBalance +=
        cache.dailyRateCollateral *
        Constants.COLLATERAL_BALANCE_PRECISION;
    amountToPay =
        marginDeposit +
        liquidationBonus +
        cache.dailyRateCollateral +
        cache.holdTokenEntranceFee;
}
```

Users can pass on `maxDailyRate` to check if the `dailyRateCollateral` has changed in between them submitting the transaction and the transaction being executed.

There is no option to check if `liquidationBonus` and `entranceFeeBps` have changed. The maximum value for `entranceFee` and `liquidationBonus` is 10%.

Given the unfortunate circumstances of a borrower taking a big loan and governance changing these parameters, the borrower might pay much more than intended.

Consider allowing the caller of the borrow function to revert in case these two parameters surpass a certain value.

### [L-02] Missing check in the `_excuteCallback` function

The `FlashLoanAggregator.flashLoan` function contains the `address(POOL) != address(0)` check. But in case the `flashLoanParams[0].protocol` is not `Protocol.AAVE` the check will be skipped. As a result, the `FlashLoanAggregator._excuteCallback` function can throw an incorrect error. This can cause difficulties with the solving of the error reason.
